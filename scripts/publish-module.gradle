apply plugin: 'maven-publish'
apply plugin: 'signing'

group = publishGroupId
version = publishVersion

afterEvaluate {
    publishing {
        publications {
            productionRelease(MavenPublication) {
                from components.productionRelease

                groupId = publishGroupId
                artifactId = publishArtifactId
                version = publishVersion

                pom {
                    name = publishArtifactId
                    description = publishDescription
                    url = 'https://github.com/processout/processout-android'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/processout/processout-android/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = rootProject.ext["developer.id"]
                            name = rootProject.ext["developer.name"]
                            email = rootProject.ext["developer.email"]
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/processout/processout-android.git'
                        developerConnection = 'scm:git:ssh://github.com/processout/processout-android.git'
                        url = 'https://github.com/processout/processout-android/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.password"],
            rootProject.ext["signing.key"]
    )
    sign publishing.publications
}
