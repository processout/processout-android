plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'kotlin-parcelize'
}

ext {
    publishArtifactId = 'processout-android-ui-core'
    publishDescription = 'ProcessOut Android SDK - UI Core'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"

android {
    namespace 'com.processout.sdk.ui.core'
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
        setBuildConfig(it)
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            getIsDefault().set(true)
        }
    }

    flavorDimensions = ['environment']
    productFlavors {
        production {
            dimension 'environment'
        }
        staging {
            getIsDefault().set(true)
            dimension 'environment'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += '-opt-in=com.processout.sdk.ui.core.annotation.ProcessOutInternalApi'
        if (project.findProperty("composeCompilerReports") == "true") {
            freeCompilerArgs += [
                    "-P", "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                    project.buildDir.absolutePath + "/compose"
            ]
        }
    }

    buildFeatures {
        buildConfig true
        compose true
    }

    publishing {
        singleVariant("productionRelease") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

@SuppressWarnings('GrMethodMayBeStatic')
def setBuildConfig(buildType) {
    buildType.buildConfigField("String", "LIBRARY_NAME", "\"ProcessOut Android SDK - UI Core\"")
    buildType.buildConfigField("String", "LIBRARY_VERSION", "\"$publishVersion\"")
}

dependencies {
    api platform("androidx.compose:compose-bom:$androidxComposeBOMVersion")
    api "androidx.compose.material3:material3"
    api "androidx.lifecycle:lifecycle-runtime-compose:$androidxLifecycleVersion"
    api "androidx.compose.ui:ui-tooling-preview"
    debugApi "androidx.compose.ui:ui-tooling"
}
