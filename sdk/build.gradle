plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.dokka'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp'
}

ext {
    publishArtifactId = 'processout-android'
    publishDescription = 'ProcessOut Android SDK'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"

android {
    namespace 'com.processout.sdk'
    compileSdk rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion

        setBuildConfig(it)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'environment'
    productFlavors {
        production {
            dimension 'environment'
        }
        staging {
            dimension 'environment'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += '-opt-in=com.processout.sdk.core.annotation.ProcessOutInternalApi'
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
            all {
                testLogging {
                    events "PASSED", "SKIPPED", "FAILED"
                }
            }
        }
    }

    publishing {
        singleVariant("productionRelease") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

@SuppressWarnings('GrMethodMayBeStatic')
def setBuildConfig(buildType) {
    buildType.buildConfigField("String", "LIBRARY_NAME", "\"ProcessOut Android SDK\"")
    buildType.buildConfigField("String", "LIBRARY_VERSION", "\"$publishVersion\"")
}

dokkaHtml {
    dokkaSourceSets {
        named("main") {
            outputDirectory.set(file("dokka/ProcessOut_Android_SDK_${publishVersion}_Documentation"))
            moduleName.set("ProcessOut Android SDK")
            moduleVersion.set(publishVersion)
            includes.from(
                    "dokka/Initialization.md",
                    "dokka/ApiExamples.md",
                    "dokka/3DS.md",
                    "dokka/NativeAlternativePaymentMethods.md",
                    "dokka/MigrationFrom3To4.md"
            )
        }
    }
}

dependencies {
    implementation "androidx.core:core-ktx:$androidxCoreVersion"
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$androidxConstraintLayoutVersion"
    implementation "androidx.activity:activity-ktx:$androidxActivityKtxVersion"
    implementation "androidx.fragment:fragment-ktx:$androidxFragmentKtxVersion"
    implementation "androidx.browser:browser:$androidxBrowserVersion"

    implementation "com.google.android.material:material:$materialVersion"

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.squareup.moshi:moshi-adapters:$moshiVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.squareup.okio:okio:$okioVersion"
    implementation "io.coil-kt:coil:$coilVersion"
    implementation "org.commonmark:commonmark:$commonMarkVersion"

    ksp "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoInlineVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "androidx.test:core-ktx:$androidTestCoreKtxVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidTestJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidTestEspressoVersion"

    // Legacy
    implementation "com.android.volley:volley:$volleyVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"
}
