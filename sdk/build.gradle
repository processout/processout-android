plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.dokka'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp'
    id 'maven-publish'
}

android {
    namespace 'com.processout.sdk'
    compileSdk rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion

        setVersion(it)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }

    flavorDimensions 'environment'
    productFlavors {
        prod {
            dimension 'environment'
        }
        staging {
            dimension 'environment'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += '-opt-in=com.processout.sdk.core.annotation.ProcessOutInternalApi'
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
            all {
                testLogging {
                    events "PASSED", "SKIPPED", "FAILED"
                }
            }
        }
    }

    publishing {
        singleVariant("prodRelease") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

def setVersion(buildType) {
    def version = file('../version.resolved').getText()
    buildType.buildConfigField("String", "LIBRARY_VERSION", "\"$version\"")
}

dokkaHtml {
    def version = file('../version.resolved').getText()
    outputDirectory.set(file("dokka/ProcessOut_Android-SDK-${version}_Documentation"))
    dokkaSourceSets {
        configureEach {
            includes.from("dokka/ProcessOut.md")
        }
    }
}

dependencies {
    implementation "androidx.core:core-ktx:$androidxCoreVersion"
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$androidxConstraintLayoutVersion"
    implementation "androidx.activity:activity-ktx:$androidxActivityKtxVersion"
    implementation "androidx.fragment:fragment-ktx:$androidxFragmentKtxVersion"

    implementation "com.google.android.material:material:$materialVersion"

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.squareup.moshi:moshi-adapters:$moshiVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.squareup.okio:okio:$okioVersion"
    implementation "io.coil-kt:coil:$coilVersion"

    ksp "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "androidx.test:core-ktx:$androidTestCoreKtxVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidTestJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidTestEspressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-web:$androidTestEspressoVersion"
    androidTestImplementation "androidx.test:rules:$androidTestRulesVersion"

    // Legacy
    implementation "com.android.volley:volley:$volleyVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"
}

publishing {
    publications {
        prodRelease(MavenPublication) {
            groupId = 'com.github.processout'
            artifactId = 'processout-android'
            version = file('../version.resolved').getText()

            afterEvaluate {
                from components.prodRelease
            }
        }
    }
}
