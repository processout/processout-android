plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.dokka'
}

ext {
    publishArtifactId = 'processout-android-checkout-3ds'
    publishDescription = 'ProcessOut Android SDK - Checkout 3DS'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"

android {
    namespace 'com.processout.sdk.checkout.threeds'
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion

        setBuildConfig(it)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions = ['environment']
    productFlavors {
        production {
            dimension 'environment'
        }
        staging {
            dimension 'environment'
        }
    }

    variantFilter { variant ->
        if (variant.getName() == "stagingRelease") {
            setIgnore(true)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += '-opt-in=com.processout.sdk.core.annotation.ProcessOutInternalApi'
    }

    publishing {
        singleVariant("productionRelease") {
            withSourcesJar()
            // FIXME: This fails build with error "PermittedSubclasses requires ASM9" while publishing to Maven Central.
            //        Test by running: ./gradlew :checkout-3ds:publishProductionReleasePublicationToMavenLocal
//            withJavadocJar()
        }
    }
}

@SuppressWarnings('GrMethodMayBeStatic')
def setBuildConfig(buildType) {
    buildType.buildConfigField("String", "LIBRARY_NAME", "\"ProcessOut Android SDK - Checkout 3DS\"")
    buildType.buildConfigField("String", "LIBRARY_VERSION", "\"$publishVersion\"")
}

dependencies {
    implementation project(path: ':sdk')
    api "com.checkout:checkout-sdk-3ds-android:$checkout3dsSdkVersion"

    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidTestJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidTestEspressoVersion"
}
