plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    namespace 'com.processout.example'
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.processout.example"
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion

        versionCode generateVersionCode()
        versionName rootProject.ext.publishVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    flavorDimensions = ['environment']
    productFlavors {
        production {
            dimension 'environment'
            setBuildConfig(it, rootProject.ext["production.project.id"], rootProject.ext["production.project.key"])
        }
        staging {
            dimension 'environment'
            applicationIdSuffix ".staging"
            setBuildConfig(it, rootProject.ext["staging.project.id"], rootProject.ext["staging.project.key"])
        }
    }

    variantFilter { variant ->
        if (variant.getName() == "stagingRelease") {
            setIgnore(true)
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += '-opt-in=com.processout.sdk.core.annotation.ProcessOutInternalApi'
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }
}

static def generateVersionCode() {
    def commitsCount = "git rev-list --first-parent --count HEAD".execute().text.trim()
    if (commitsCount.empty) throw new RuntimeException("Could not generate version code.")
    return commitsCount.toInteger()
}

@SuppressWarnings('GrMethodMayBeStatic')
def setBuildConfig(buildType, String projectId, String projectKey) {
    buildType.buildConfigField("String", "PROJECT_ID", "\"$projectId\"")
    buildType.buildConfigField("String", "PROJECT_KEY", "\"$projectKey\"")
}

dependencies {
    implementation project(path: ':sdk')
    implementation project(path: ':checkout-3ds')

    implementation "androidx.core:core-ktx:$androidxCoreVersion"
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$androidxConstraintLayoutVersion"
    implementation "androidx.activity:activity-ktx:$androidxActivityKtxVersion"
    implementation "androidx.fragment:fragment-ktx:$androidxFragmentKtxVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$androidxNavigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$androidxNavigationVersion"
    implementation "androidx.recyclerview:recyclerview:$androidxRecyclerViewVersion"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$androidxSwipeRefreshLayoutVersion"

    implementation "com.google.android.material:material:$materialVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoInlineVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "androidx.test:core-ktx:$androidTestCoreKtxVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidTestJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidTestEspressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-web:$androidTestEspressoVersion"
    androidTestImplementation "androidx.test:rules:$androidTestRulesVersion"

    // Legacy
    androidTestImplementation "com.android.volley:volley:$volleyVersion"
    androidTestImplementation "com.google.code.gson:gson:$gsonVersion"
}
