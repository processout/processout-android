plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.dokka'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp'
}

ext {
    publishArtifactId = 'processout-android-ui'
    publishDescription = 'ProcessOut Android SDK - UI'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"

android {
    namespace 'com.processout.sdk.ui'
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
        setBuildConfig(it)
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            getIsDefault().set(true)
        }
    }

    flavorDimensions = ['environment']
    productFlavors {
        production {
            dimension 'environment'
        }
        staging {
            getIsDefault().set(true)
            dimension 'environment'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += '-opt-in=com.processout.sdk.core.annotation.ProcessOutInternalApi'
        freeCompilerArgs += '-opt-in=com.processout.sdk.ui.core.annotation.ProcessOutInternalApi'
        if (project.findProperty("composeCompilerReports") == "true") {
            freeCompilerArgs += [
                    "-P", "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                    project.buildDir.absolutePath + "/compose"
            ]
        }
    }

    buildFeatures {
        buildConfig true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = rootProject.ext.androidxComposeCompilerVersion
    }

    publishing {
        singleVariant("productionRelease") {
            withSourcesJar()
            // FIXME: This fails build with error "PermittedSubclasses requires ASM9" while publishing to Maven Central.
            //        Test by running: ./gradlew :ui:publishProductionReleasePublicationToMavenLocal
//            withJavadocJar()
        }
    }
}

@SuppressWarnings('GrMethodMayBeStatic')
def setBuildConfig(buildType) {
    buildType.buildConfigField("String", "LIBRARY_NAME", "\"ProcessOut Android SDK - UI\"")
    buildType.buildConfigField("String", "LIBRARY_VERSION", "\"$publishVersion\"")
}

dependencies {
    api project(path: ':sdk')
    api project(path: ':ui-core')

    api "androidx.activity:activity-compose:$androidxActivityVersion"
    api "androidx.lifecycle:lifecycle-viewmodel-compose:$androidxLifecycleVersion"

    api "com.google.android.gms:play-services-wallet:$gmsWalletVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$kotlinxCoroutinesPlayServicesVersion"
    implementation "com.googlecode.libphonenumber:libphonenumber:$libphonenumberVersion"
    implementation "io.coil-kt:coil-compose:$coilVersion"

    ksp "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
}
